import org.gradle.internal.os.OperatingSystem;

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

group = 'com.jme3'
version = '2.0.0'

compileJava {
    options.encoding = "UTF-8"
    options.incremental = true
    options.compilerArgs.addAll([
        "--add-exports",
        "javafx.graphics/com.sun.javafx.embed=ALL-UNNAMED"
    ])
    options.compilerArgs.addAll([
        "--add-exports",
        "javafx.graphics/com.sun.javafx.stage=ALL-UNNAMED"
    ])
    options.compilerArgs.addAll([
        "--add-exports",
        "javafx.graphics/com.sun.javafx.cursor=ALL-UNNAMED"
    ])
    options.compilerArgs.addAll([
        "--add-exports",
        "javafx.graphics/com.sun.glass.ui=ALL-UNNAMED"
    ])
    options.compilerArgs.addAll([
        "--add-exports",
        "javafx.graphics/com.sun.javafx.application=ALL-UNNAMED"
    ])
}

sourceCompatibility = 1.10
targetCompatibility = 1.10

ext.rlibVersion = "7.0.0"
ext.bintrayVersion = version
ext.javaFxVersion = '13'

def jme3 = [v: '3.2.0-stable', g: 'org.jmonkeyengine']

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
    maven {
        url "https://jitpack.io"
    }
    maven {
        url  "https://dl.bintray.com/javasabr/maven"
    }
}


bintray {
    user = System.getProperty('bintrayUser')
    key = System.getProperty('bintrayKey')
    publications = ['LibArtifact']
    pkg {
        repo = 'maven'
        name = 'com.jme3.jfx'
        licenses = ['Apache-2.0']
        websiteUrl = 'https://github.com/JavaSaBr/JME3-JFX'
        vcsUrl = 'https://github.com/JavaSaBr/JME3-JFX.git'
        issueTrackerUrl = 'https://github.com/JavaSaBr/JME3-JFX/issues'
        githubRepo = 'JavaSaBr/JME3-JFX'
        githubReleaseNotesFile = 'README.md'
        version {
            name = bintrayVersion
            desc = bintrayVersion
            released  = new Date()
        }
    }
}

dependencies {
    compile group: 'org.jetbrains', name: 'annotations', version: '15.0'
    compile "com.github.wizzardo:RLib:${rlibVersion}"
    compile "${jme3.g}:jme3-core:${jme3.v}"
    compile "${jme3.g}:jme3-lwjgl3:${jme3.v}"


    def targetArch = project.hasProperty('targetArch') ? project.property("targetArch") : 'x64'

    def osName = ""
    if (OperatingSystem.current().isLinux()) {
        osName = "linux"
    } else if (OperatingSystem.current().isMacOsX()) {
        osName = "mac"
    } else if (OperatingSystem.current().isWindows()) {
        osName = "win"
    }

    if (targetArch == "x64" || targetArch == "x86") {
        implementation "org.openjfx:javafx-base:$javaFxVersion:${osName}"
        implementation "org.openjfx:javafx-controls:$javaFxVersion:${osName}"
        implementation "org.openjfx:javafx-graphics:$javaFxVersion:${osName}"
    }
}

javadoc {
    failOnError = false
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
